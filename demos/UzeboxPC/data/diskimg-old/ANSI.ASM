;
;**************************************************
;* 						  *
;*  USER-MODIFYABLE ROUTINES AND CONSTANTS FOR    *
;*  HARDWARE-DEPENDENT TERMINAL CHARACTERISTICS   *
;*  AND FUNCTIONS USED BY WORD-MASTER.            *
;*                       RELEASE 1.07   2/21/80   *
;**************************************************
;
; THIS VERSION WORKS FOR ANSI TERMINALS LIKE VT-100
;
; UDO MUNK
;

	ORG	180H

OUTCHR	EQU	01EFH
MEMORY	EQU	29B8H

;
; *** SUBROUTINE TO CLEAR SCREEN AND HOME CURSOR ***
;
; SEND ESCAPE, AND '[2J'.
;
CLRSCRN: MVI A,1BH	;(1) GET ESCAPE CHARACTER
    	CALL OUTCHR	;SEND IT TO TERMINAL
      	MVI A,'['	;(2) GET CLEAR-SCREEN CODE
    	CALL OUTCHR	;SEND IT TO TERMINAL
	MVI A,'2'
	CALL OUTCHR
	MVI A,'J'
	JMP PATCHA	;JUMP TO PATCH AREA for HOME CURSOR
	NOP
	NOP

;
; *** SUBROUTINE TO POSITION CURSOR AT      ***
; ***     LINE L (0=TOP), COLUMN H (0=LEFT) ***
;
;ANSI VERSION:
; SENDS ESCAPE, '[', ASCII Y(L)+1, ';', ASCII X(H)+1, 'H'
;
TCURSOR: MVI A,1BH! CALL OUTCHR	;SEND ESCAPE
	MVI A,'['! CALL OUTCHR	;SEND CURSOR ADDRESSING COMAND
	MOV A,L ! INR A		;GET ROW + 1
	CALL PATCHB		;SEND ROW NUMBER IN ASCII
	MVI A,';' ! CALL OUTCHR	;SEND ';'
  	MOV A,H ! INR A		;GET COL + 1
	CALL PATCHB		;SEND COL NUMBER IN ASCII
	MVI A,'H' ! JMP OUTCHR	;SEND 'H' AND DONE
	NOP
	NOP
	NOP

	;NOTE: BACKSPACE ROUTINE THAT WAS IN
	;PRIOR RELEASES IS NO LONGER NEEDED.

; **** MODIFYABLE CONSTANTS *****

 ;PBEGMEM POINTS TO BEGINNING OF MEMORY TO USE
 ;FOR EDIT BUFFER AND SCRATCHPAD. IF SPACE IS NEEDED
 ;FOR PATCHES, PUT THEM WHERE THIS POINTS AND
 ;INCREASE THIS POINTER. REMEMBER TO USE A LARGE
 ;ENOUGH "SAVE" COMMAND!

;PBEGMEM: DW MEMORY
PBEGMEM: DW 2A00H	;WE NEED MORE PATCH SPACE FOR ANSI

 ;SCREEN SIZE: TAKEN FROM THE FOLLOWING,
 ;EXCEPT SET AUTOMATICALLY TO MATCH HARDWARE
 ;VALUES WHEN IMSAI VIO VIDEO DISPLAY IS IN USE
 ;(DETECTED BY PRESENCE OF THE VIO ROM AT PROPER
 ;ADDRESS, AND CON: IOBYTE FIELD = 2 OR 3).

HITE:	DB 24	;MUST BE EXACT SCREEN HEIGHT IN LINES
WID:	DB 80	;MUST BE <= EXACT SCREEN WIDTH

 ;EREOL CONTAINS THE CHARACTER(S) TO ERASE SCREEN
 ;TO END-OF-LINE WITHOUT MOVING CURSOR, IF SUCH A
 ;CHARACTER IS AVAILABLE IN THE TERMINAL HARDWARE.
 ;IF 0, WILL BE SIMULATED BY SOFTWARE.
 ;AUTOMATICALLY SET TO CTL-U WHEN VIO IS IN USE.

EREOL:	DB 0	;(FIRST) CHARACTER, OR 0 IF NONE
	DB 0	;SECOND CHARACTER IF TERMINAL
		;...REQUIRES 2-CHARACTER
		;...SEQUENCE, ELSE A 0.

 ;NOVIO: IF NON-0, WILL NOT LOOK FOR IMSAI VIO.
 ;PATCH NON-0 IF VIO PROVIOSIONS INTERFERE WITH
 ;YOUR TERMINAL.

NOVIO:	DB 0FFH		;PATCHED FOR ANSI
	DB 0,0,0	;RESERVED FOR EXPANSION

;DELAYS EXECUTED AFTER VARIOUS TERMINAL FUNCTIONS,
;BEFORE NEXT CHARACTER IS SENT TO TERMINAL. THESE
;ALLOW TIME FOR TERMINAL TO RESPOND, AS REQUIRED
;BY SOME TERMINALS WHEN USED AT HIGH BAUD RATES.
;INCREASE IF YOU EXPERIENCE, FOR EXAMPLE, LOSS OF
;CHARACTERS AFTER CLEAR SCREEN.  EACH DELAY IS
;APPROX NUMBER OF MILLISECONDS ON 4MHZ PROCESSOR;
;DELAY IS TWICE AS LONG AS SHOWN FOR 2MHZ 8080.

DELCLR:	DB 25	;DELAY AFTER CLEAR SCREEN: 25+ MSEC.
DELCUS:	DB 10	;DELAY AFTER POSITION CURSOR: 10+MSEC.
DELERE:	DB 5	;DELAY AFTER ERASE TO EOL: 5+ MSEC.

PATCHA:	CALL OUTCHR		;CURSOR HOME
	MVI A,1BH ! CALL OUTCHR	;SEND ESC
	MVI A,'[' ! CALL OUTCHR	;AND '[1;1H'
	MVI A,'1' ! CALL OUTCHR
	MVI A,';' ! CALL OUTCHR
	MVI A,'1' ! CALL OUTCHR
	MVI A,'H' ! JMP OUTCHR

	PAGE

	ORG	MEMORY

PATCHB:				;CONVERT BINARY TO ASCII
	PUSH H
	MVI L,0FFH
PL1:	INR L
	SUI 0AH
	JNC PL1
	ADI 0AH
	MOV H,A
	MOV A,L
	ORA A
	JZ PL2
	ORI 30H
	CALL OUTCHR
PL2:	MOV A,H
	ORI 30H
	POP H
	JMP OUTCHR

	END
